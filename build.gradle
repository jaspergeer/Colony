import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.12.7'
}

group 'com.jaspergeer'
version '1.0'

mainClassName = 'com.jaspergeer.app.Main'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation files('libs/jfoenix-9.0.10.jar')
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jar {
    manifest {
        attributes 'Main-Class': 'com.jaspergeer.app.Main'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

runtime {
    jpackage {
        OperatingSystem os = DefaultNativePlatform.currentOperatingSystem;
        imageName = 'Colony'
        installerName = 'ColonyInstaller'
        imageOptions = ['--icon', 'src/main/resources/colony.icns']
        if (os.isMacOsX()) {
            installerType = 'pkg'
            installerOutputDir = file("$buildDir/mac-installer")
        } else if (os.isWindows()) {
            installerType = 'msi'
            installerOutputDir = file("$buildDir/win-installer")
            imageOptions = ['--icon', 'src/main/resources/colony.ico']
        } else if (os.isLinux()) {
            installerType = 'rpm'
            installerOutputDir = file("$buildDir/linux-installer")
        } else {
            installerOutputDir = file("$buildDir/otherOS-installer")
        }
    }
}

if(JavaVersion.current() != JavaVersion.VERSION_14){
    throw new GradleException("This build must be run with Java 14 or higher")
}